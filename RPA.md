# RPA

## 中文脚本基础

### 1， 输入模板

组件模板()”表示组件名称,括号里面填写“参数定义”的参数名,多个参数用“,”分割。

类似于面向对象语言中的函数名称及参数，里面的参数在脚本中的**参数定义**中定义。

注：这边输入的参数只需参数名，无需参数类型。具体原因请看**参数定义**部分。

```java
//函数名称及参数
{$I 输入模板.run}
小站双向通信系统_参数更新通知_张鼎(入参_S_程序路径,入参_S_证书,入参_S_密码)
```

### 2，帮助信息

用于说明当前组件的大致作用或一些注意事项

### 3，参数定义

“:”左边是参数名称,“:”右边是参数类型,默认“字符串”,可填写其他类型,定义多个参数时,需要换行。

也就是设定入参，函数里要使用到的参数在这边定义，变量命名请规范。详细规范请见：K-RPA命名规范及开发原则

```java
//参数定义  定义好的参数才能使用在函数中
{$I 参数定义.run}
入参_S_程序路径:字符串
入参_S_证书:字符串      
入参_S_密码:字符串
```

### 4，返回值定义

：左边不可修改，统一命名“返回”，用来储存组件执行后的返回值，“：”右边是参数
类型，默认“字符串”，可填写其他类型

### 5，逻辑主体

书写组件实现逻辑, “返回=设置指标('正常,'数据,'描述')”不可删除

“:”左边不可修改,统一命名“返回”,用来储存组件执行后的返回值,“:”右边是参数类型,默认“字符串”,可填写其他类型

也就是函数的主要内容。

```java
//逻辑主要内容，顺序执行
{$I 函数主体.run}
//封装好的方法（等待进程）
//脚本是马上执行的，而程序不是马上执行的，所以要让脚本等待程序启动
I_程序 = 等待进程(入参_s_程序路径,5)
if(I_程序 = 0) 
[
  出参_S_返回 = 设置指标('Error','启动失败','进程未在规定时间内启动！')
  日志('程序出错，状态码为：'+出参_S_返回)
  Exit 
]
出参_S_返回 = 设置指标('Success', '流程正常', '程序正常启动')
Trace('程序正常，状态码为：'+出参_S_返回)
```

### 6，调试开始

运行时请确认入参

“调用组件()”只需要往括号依次填写“参数定义”对应的参数值,多个参数值用“,”隔开注:这里传入的参数值只是用来调试组件,外部调用时以外部传入的参数值为主

### 7，调试结束

### 注意事项

1.符号与中文参数不要黏在一起，使用空格隔开。

```
I_程序 = 等待进程(入参_s_程序路径,5)
```

2.//判断符为 ''='',而不是''==''，不等号是 '<>'。

3.代码块不是使用''{}'' ，而是''[]''。

```java
//判断符为 '='  而不是'==' 不等号是 '<>'
if(I_程序 = 0) 
//注意：这里不是{} ，而是[]。    
[
  出参_S_返回 = 设置指标('Error','启动失败','进程未在规定时间内启动！')
  日志('程序出错，状态码为：'+出参_S_返回)
  Exit 
]
```

4.每一个风险请打印日志，减少调试时间。

```java
//启动信息如果不正确，报错 
if(B_证书标识是否正确 = false)
[
  出参_S_返回 = 设置指标('严重','启动标识错误','启动标识错误')
  日志('程序出错，错误日志：'+出参_S_返回)
  Exit
]
```

### 基本案例

小站双向通信系统_登录

```java
//函数名称及参数
{$I 输入模板.run}
小站双向通信系统_登录_张鼎(入参_S_程序路径,入参_S_证书,入参_S_密码)

{$I 帮助信息.run}

//参数定义  定义好的参数才能使用在函数中
{$I 参数定义.run}
入参_S_程序路径:字符串
入参_S_证书:字符串      
入参_S_密码:字符串

//返回值定义 也就是return
{$I 返回定义.run}
出参_S_返回:字符串

//逻辑主要内容，顺序执行
{$I 函数主体.run}
//1.运行程序
运行程序(入参_S_程序路径,'','',显示)
//脚本是马上执行的，而程序不是马上执行的，所以要让脚本等待程序启动
//设置函数内局部变量：程序的状态码
I_小站双向通信系统程序 = 等待进程(入参_S_程序路径,5)
//风险：判断程序是否启动成功
if(I_小站双向通信系统程序 = 0) //判断符为 '='  而不是'=='
//注意：这里不是{} ，而是[]。    
[
  //程序状态码为0，则程序在规定时间内未启动成功，return一个报错
  出参_S_返回 = 设置指标('Error','启动失败','进程未在规定时间内启动！')
  //输出错误日志
  日志('程序出错，错误日志：'+出参_S_返回)
  //退出脚本
  Exit 
]

//2.选择证书
//使用等待窗口，因为窗口是在程序启动后的一小段时间后出现的，所以需要等待
//使用查看窗口工具（按住右上图标选择相应窗口） 获取两个窗口串传入
I_证书选择窗口 = 等待窗口(I_小站双向通信系统程序, ['#32770','ListBox'],['证书选择','(2)'], 5)
//风险：证书选择窗口是否正常
if(I_证书选择窗口 = 0)
[
  出参_S_返回 = 设置指标('Error','证书选择窗口获取失败','证书选择窗口未在规定时间被获取！')
  日志('程序出错，错误日志：'+出参_S_返回)
  Exit 
]

//获取窗口中列表的行数
I_证书选择窗口行数 = 获取列表行数(I_证书选择窗口)
//设定变量用于后期验证
B_证书选择行是否选中 = false 
//遍历窗口列表内容
for I = 0 to I_证书选择窗口行数
[
  //获取第一行内容
  S_证书选择窗口行内容 = 获取列表单行(I_证书选择窗口,I)
  //去除两端空格，保证验证结果
  S_证书选择窗口行内容 = Trim(S_证书选择窗口行内容)
  //验证获取的内容是否是我们需要的内容（软证书）
  if(S_证书选择窗口行内容 = 入参_S_证书)
  [
    //确认需要行，选中该行
    B_证书选择结果 = 选中列表单行(I_证书选择窗口,I)
    B_证书选择行是否选中 = true
    //获取到想要的结果后 退出循环
    break
  ]
]

//风险：证书选择行是否可以选中
if(I_证书选择窗口 = 0)
[
  出参_S_返回 = 设置指标('Error','证书选择失败','未能成功选择证书：' + 入参_S_证书)
  日志('程序出错，错误日志：'+出参_S_返回)
  Exit 
]

//风险：证书确认按钮是否可以选中
I_证书确认按钮 = 等待窗口(I_小站双向通信系统程序,['#32770','Button'],['证书选择','(3)'],5)
B_是否成功确认证书 = 单击按钮(I_证书确认按钮)
if(B_是否成功确认证书 = false)
[
  出参_S_返回 = 设置指标('Error','证书确认失败','未能成功确认证书：' + 入参_S_证书)
  日志('程序出错，错误日志：'+出参_S_返回)
  Exit
]

//3.密码验证
//风险：密码框是否存在
I_密码输入窗口 = 等待窗口(I_小站双向通信系统程序,['#32770','Edit'],['验证密码','(2)'],5)
if(I_密码输入窗口 = 0)
[
  出参_S_返回 = 设置指标('Error','密码输入框获取失败','未能成功获取密码输入框!')
  日志('程序出错，错误日志：'+出参_S_返回)
  Exit 
]

//风险： 密码输入是否正常
B_密码输入结果 = 设置文本内容(I_密码输入窗口,入参_S_密码)
if(B_密码输入结果 = false)
[
  出参_S_返回 = 设置指标('Error','密码输入失败','未能成功输入密码!')
  日志('程序出错，错误日志：'+出参_S_返回)
  Exit 
]

//风险： 密码确认按钮是否正常
I_密码确认按钮 = 等待窗口(I_小站双向通信系统程序,['#32770','Button'],['验证密码','(4)'],5)
B_是否成功确认密码 = 单击按钮(I_密码确认按钮)
if(B_是否成功确认密码 = false)
[
  出参_S_返回 = 设置指标('Error','密码确认失败','未能成功确认密码！')
  日志('程序出错，错误日志：'+出参_S_返回)
  Exit
]

//4.启动完成
// 风险：程序启动登录是否正常
I_程序状态信息窗口 = 等待窗口(I_小站双向通信系统程序,['#32770','ListBox'],['深圳证券通信--小站双向通信系统','(11)'],5)
if(I_程序状态信息窗口 = 0)
[
  出参_S_返回 = 设置指标('Error','程序状态信息窗口获取失败','未能成功获取程序状态信息窗口!')
  日志('程序出错，错误日志：'+出参_S_返回)
  Exit 
]

//获取窗口中列表的行数
I_程序状态信息窗口行数 = 获取列表行数(I_程序状态信息窗口)
//设定变量用于后期验证
B_证书标识是否正确 = false 
//验证启动成功标识是否存在
B_启动成功标识是否正确 = false
//优化验证：多次获取，防止因程序启动过慢导致获取错误信息
for I = 0 to 3
[
  //输出格式化后的文本，动态信息显示
  输出信息(Format('正在第%d次查找标识信息，耗时%d秒',[I+1,I*5]))
  //反向遍历窗口列表内容
  for J =  I_程序状态信息窗口行数 - 1 downto 0
  [
    //获取第一行内容
    S_程序状态信息窗口行内容 = 获取列表单行(I_程序状态信息窗口,J)
    //去除两端空格，保证验证结果
    S_程序状态信息行内容 = Trim(S_证书选择窗口行内容)
    //验证获取的内容是否是我们需要的内容（启动成功）
    if(pos('启动成功',S_程序状态信息行内容) > 0)
    [
      B_证书标识是否正确 = true
      //获取到想要的结果后 退出循环
      break
    ]
  ]
  //遍历窗口列表内容
  for N = 0 to I_程序状态信息窗口行数
  [
    //获取第一行内容
    S_程序状态信息窗口行内容 = 获取列表单行(I_程序状态信息窗口,N)
    //去除两端空格，保证验证结果
    S_程序状态信息行内容 = Trim(S_证书选择窗口行内容)
    //验证获取的内容是否是我们需要的内容（软证书）
    if(S_程序状态信息行内容 = 入参_S_证书)
    [
      B_证书标识是否正确 = true
      //获取到想要的结果后 退出循环
      break
    ]
  ]
  if(B_证书标识是否正确 = true)
  [
      if(B_证书标识是否正确 = true)
      [
        //获取到想要的结果后 退出循环
        break
      ]
  ]
  等待(5000)
]

//启动信息如果不正确，报错 
if(B_证书标识是否正确 = false)
[
  出参_S_返回 = 设置指标('Error','启动标识错误','启动标识错误')
  日志('程序出错，错误日志：'+出参_S_返回)
  Exit
]
//证书选择信息如果不正确，报错 
if(B_证书标识是否正确 = false)
[
  出参_S_返回 = 设置指标('Error','证书标识错误','证书标识错误：当前标识：'+S_程序状态信息行内容 +'正确标识应为：'+入参_S_证书)
  日志('程序出错，错误日志：'+出参_S_返回)
  Exit
]

//状态不为0则程序正常
出参_S_返回 = 设置指标('Success', '流程正常', '程序正常启动')
Trace('程序正常，状态：'+出参_S_返回)
//调试部分 给函数相应的参数，然后启动
{$I 调试开始.run}
调用组件('S:\Work\RPA\Project\Test\深圳证券通信--小站双向通信系统\bin\TWComm.exe','软加密','123456')
{$I 调试结束.run}
```

## 流程设计

**跑流程的时候先检查下面几点要素**

1.组件流程先测试能够跑通。

2.组件审核通过。

3.代理机Agent启动正常。

4.如果组件入参更新，流程也请更新。
